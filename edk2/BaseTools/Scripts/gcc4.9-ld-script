/* OUTPUT_FORMAT(efi-bsdrv-x86_64) */
SECTIONS
{
  /* . = 0 + SIZEOF_HEADERS; */
  . = PECOFF_HEADER_SIZE;
  .text ALIGN(CONSTANT(COMMONPAGESIZE)) :
  {
    *(.text .stub .text.* .gnu.linkonce.t.*)
    *(.rodata .rodata.* .gnu.linkonce.r.*)
    *(.got .got.*)
  }
  .data ALIGN(CONSTANT(COMMONPAGESIZE)) :
  {
    *(
      .data .data.* .gnu.linkonce.d.*
      .bss .bss.*
      *COM*
    )
  }
  .eh_frame ALIGN(CONSTANT(COMMONPAGESIZE)) :
  {
    KEEP (*(.eh_frame))
  }
  .rela ALIGN(CONSTANT(COMMONPAGESIZE)) :
  {
    *(.rela .rela.*)
  }
  /DISCARD/ : {
    *(.note.GNU-stack) *(.gnu_debuglink)
    *(.interp)
    *(.dynsym)
    *(.dynstr)
    *(.dynamic)
    *(.hash)
    *(.comment)
  }
}

